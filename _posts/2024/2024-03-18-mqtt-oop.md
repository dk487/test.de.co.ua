---
title: <abbr>MQTT</abbr> та <abbr>ООП</abbr>
date: 2024-03-18 15:17:07 +02:00
opengraph_generator:
  template: clickbait01
  label_row1: MQTT та ООП
---

Об'єднав код з двох недавніх постів у одне ціле. Тепер моя Arduino [керується з Node-RED по MQTT][2] і у неї всередині крутиться дуже [дивний об'єктно-орієнтований код][1].


## Код

Тут публікую лише головну частину прошивки, решта кода в [свалці на Github][3].

```cpp
#include <Arduino.h>

#include "loop_job.h"
#include "mqtt_io.h"
#include "debouncer.h"
#include "blinking_led.h"

BlinkingLED myBlinker(LED_BUILTIN);

const uint8_t myButtonPin = 2;
Debouncer myButton;

MessageHub mqtt;

TopicSubscription topics[] = {
  {
    "dev/board05/led/set",
    [](char *payload) {
      myBlinker.enabled = (payload[0] == '1');
    }
  }
};

class MyAnalogReader: public ScheduledLoop {
public:
  uint8_t pin;
protected:
  void runScheduled() {
    mqtt.send("dev/board05/A0", analogRead(this->pin));
  }
} myA0;

void setup() {
  Serial.begin(9600);

  mqtt.begin();
  mqtt.subscriptions = topics;
  mqtt.subscriptionsCount = sizeof(topics) / sizeof(topics[0]);

  myBlinker.setup();
  myBlinker.runPeriod = 250000; // 250ms

  pinMode(myButtonPin, INPUT_PULLUP);
  myButton.readingSource = []() {
    return digitalRead(myButtonPin);
  };
  myButton.onFall = []() {
    myBlinker.enabled = !myBlinker.enabled;
    mqtt.send("dev/board05/led", myBlinker.enabled ? 1 : 0);
  };

  pinMode(A0, INPUT);
  myA0.pin = A0;
  myA0.runPeriod = 250000; // 250ms
}

void loop() {
  myBlinker.loop();
  myButton.loop();
  mqtt.loop();
  myA0.loop();
}
```

Чи краса?


## Node-RED

Той самий дашборд.

![Знімок екрану дашборду Node-RED](/uploads/2024_nodered_dashboard.png)

Майже той самий flow. Тільки додав на вхід до вимикача вхідне значення `dev/board05/led`.

Було:

![Знімок екрану Node-RED зі сконфігуро́ваними потоками](/uploads/2024_nodered_flow.png)

Стало:

![Ще один екрану Node-RED зі сконфігуро́ваними потоками](/uploads/2024_nodered_flow_2.png)

Можна вмикати-вимикати блималку за допомогою кнопки, можна вмикати-вимикати з дашборда. Дашборд показує актуальний стан блималки. Без Node-RED (автономно) кнопка продовжує працювати.


## Кабель

А, ледь не забув! Додав дуже важливий рядок з `pkill` в скрипт для <abbr>MQTT</abbr>.

```sh
#!/bin/sh
while /bin/true; do
  socat -d -d /dev/ttyUSB0,b9600 exec:./mqtt_io
  pkill -A -s 0
  sleep 5
done
```

Кабель можна від'єднати і приєднати, все працюватиме далі. Але раніше лишалися зайві процеси, яких з кожним перепідключенням кабеля ставало дедалі більше. Тепер без цієї фігні.


[1]: /2024/02/08/arduino-blinking-led.html
[2]: /2024/02/20/arduino-mqtt.html
[3]: https://github.com/kastaneda/arduino_sandbox/tree/master/very_silly_serial/another_silly_sketch
